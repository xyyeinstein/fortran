FUNCTION SGN(I)
  INTEGER SGN,I
  SGN=1; IF(I<0)SGN=-1
  RETURN
END FUNCTION SGN


!--------------------Linear Algebra-----------------------------------------------
FUNCTION DDET(N, A)
   IMPLICIT NONE
   INTEGER I, N, INFO
   INTEGER IPVT(N)
   REAL*8 A(N,N),B(N,N),DDET
   IF(N<=0)STOP 'INVALID DIM @ DDET'
   IF(N==1)THEN; DDET=A(1,1); RETURN; END IF   
   B=A;  CALL DGETRF(N,N,B,N,IPVT,INFO)
   IF(INFO/=0)THEN; DDET=0; RETURN; END IF          
   INFO=1; DDET=0
   DO I=1, N
      IF(IPVT(I).NE.I)INFO=-INFO
	  IF(B(I,I)<0)THEN; INFO=-INFO; B(I,I)=-B(I,I); END IF
	  DDET=DDET+LOG(B(I,I))
   END DO
   DDET=EXP(DDET); IF(INFO<0)DDET=-DDET
   RETURN
END FUNCTION DDET

FUNCTION ZDET(N, A)
   IMPLICIT NONE
   INTEGER I, N, INFO
   INTEGER IPVT(N)
   COMPLEX*16 A(N,N),B(N,N),ZDET
   IF(N<=0)STOP 'INVALID DIM @ ZDET'
   IF(N==1)THEN; ZDET=A(1,1); RETURN; END IF
   B=A;  CALL ZGETRF(N,N,B,N,IPVT,INFO)
   IF(INFO/=0)THEN; ZDET=0; RETURN; END IF          
   INFO=1; ZDET=0
   DO I=1, N
      IF(IPVT(I)/=I)INFO=-INFO
	  ZDET=ZDET+LOG(B(I,I))
   END DO
   ZDET=EXP(ZDET); IF(INFO<0)ZDET=-ZDET
   RETURN
END FUNCTION ZDET

SUBROUTINE DINVERT(N,A)
  IMPLICIT NONE
  INTEGER N
  REAL*8 A(N,N), WORK(2*N)
  INTEGER INFO, IPIV(N), LWORK
  IF(N<=0)STOP 'INVALID N @ DINVERT'
  IF(N==1)THEN; A=1/A; RETURN; END IF
  CALL DGETRF(N, N, A, N, IPIV, INFO)
  IF(INFO/=0)STOP 'LU @ DINVERT DETECTED A SINGULARITY'
  LWORK=2*N
  CALL DGETRI(N, A, N, IPIV, WORK, LWORK, INFO)
  IF(INFO/=0)STOP 'DINVERT FAILED'
  RETURN
END SUBROUTINE DINVERT

SUBROUTINE ZINVERT(N,A)
  IMPLICIT NONE
  INTEGER N
  COMPLEX*16 A(N,N), WORK(2*N)
  INTEGER INFO, IPIV(N), LWORK
  IF(N<=0)STOP 'INVALID DIM @ ZINVERT'
  IF(N==1)THEN; A=1/A; RETURN; END IF
  CALL ZGETRF(N, N, A, N, IPIV, INFO)
  IF(INFO/=0)PRINT*, 'LU@INVERSE DETECTED A SINGULARITY'
  LWORK=2*N
  CALL ZGETRI(N, A, N, IPIV, WORK, LWORK, INFO)
  IF(INFO/=0)STOP 'ZINVERT FAILED'
  RETURN
END SUBROUTINE ZINVERT

SUBROUTINE DHEIGEN(N,A,EVAL)
  IMPLICIT NONE
  INTEGER N,INFO
  REAL*8 A(N,N),EVAL(N),WORK(3*N)

  IF(N<=0)STOP 'INVALID N @ DHEIGEN'
  IF(N==1)THEN; EVAL(1)=A(1,1); A=1; RETURN; END IF

  CALL DSYEV('V','U',N,A,N,EVAL,WORK,3*N,INFO)
  IF(INFO/=0)STOP 'DHEIGEN FAILED'
  RETURN
END SUBROUTINE DHEIGEN

SUBROUTINE ZHEIGEN(N,A,EVAL)
  IMPLICIT NONE
  INTEGER N,LWORK,INFO
  DOUBLE PRECISION RWORK(N*3),EVAL(N)
  COMPLEX*16 WORK(N*2),A(N,N)

  IF(N<=0)STOP 'INVALID N @ ZHEIGEN'
  IF(N==1)THEN; EVAL(1)=A(1,1); A=1; RETURN; END IF

  LWORK=N*2
  !CALL CHEEV('V','U',N,A,N,EVAL,WORK,LWORK,RWORK,INFO)
  CALL ZHEEV('V','U',N,A,N,EVAL,WORK,LWORK,RWORK,INFO)
  IF(INFO/=0)STOP 'ZHEIGEN FAILED'
  RETURN
END SUBROUTINE ZHEIGEN

SUBROUTINE DSVD( M,N,A,U,S,V )
  IMPLICIT NONE
  INTEGER M,N,LWORK,INFO
  REAL*8 A(M,N),U(M,M),S(*),V(N,N)
  REAL*8, ALLOCATABLE, DIMENSION (:) ::WORK
  LWORK=MAX(1,3*MIN(M,N) + MAX(M,N), 5*MIN(M,N) )+100
  ALLOCATE( WORK(LWORK) )
  CALL DGESVD ('A','A',M,N,A,M,S,U,M,V,N,WORK,LWORK,INFO )
  IF(INFO/=0) STOP 'DSVD FAILED'
  DEALLOCATE(WORK)
  RETURN
END SUBROUTINE DSVD

SUBROUTINE ZSVD( M,N,A,U,S,V )
  IMPLICIT NONE
  INTEGER M,N,LWORK,INFO
  COMPLEX*16 A(M,N),U(M,M),V(N,N)
  REAL*8 S(*)
  COMPLEX*16, ALLOCATABLE :: WORK(:)
  COMPLEX*16, ALLOCATABLE :: RWORK(:)
  LWORK=MAX(1,3*MIN(M,N)+MAX(M,N),5*MIN(M,N))+100
  ALLOCATE(WORK(LWORK),RWORK(MIN(M,N)*5))
  CALL ZGESVD ('A','A',M,N,A,M,S,U,M,V,N,WORK,LWORK,RWORK,INFO)
  IF(INFO/=0) STOP 'ZSVD FAILED'
  DEALLOCATE(WORK,RWORK)
  RETURN
END SUBROUTINE ZSVD

SUBROUTINE ZFFT(N,Z,INFO)
  IMPLICIT NONE
  INTEGER N,M,INFO
  COMPLEX*16 Z(N),EXPA,ZT,ONE
  REAL*8 E,A
  INTEGER I,J,K,L,N1,N2
  IF(ABS(INFO)/=1)STOP 'INFO MUST BE EITHER 1 OR -1 @ ZFFT.'
  !INFO CONTROLLS THE FFT FACTOR EXP(-INFO*ONE*W*T)
  
  M=0; I=1;  DO WHILE (I<N); I=I*2; M=M+1; END DO
  IF(I/=N)STOP 'N /= 2^M @ ZFFT'
  
  !--------------MAIN FFT LOOPS----------------------
  ONE=CMPLX(0,1); N2=N
  DO K =1,M
     N1=N2;  N2=N2/2;  E=6.283185307179586/N1;  A=0   
     DO J=1,N2;  EXPA=EXP(-INFO*ONE*A);  A=J*E
     DO I=J, N, N1;  L=I+N2
        ZT=Z(I)-Z(L);  Z(I)=Z(I)+Z(L);  Z(L)= EXPA*ZT
     END DO; END DO
  END DO
  !------------DIGIT REVERSE COUNTER-----------------
  J=1;  N1=N-1
  DO I=1,N1
     IF(I<J)THEN; ZT=Z(J); Z(J)=Z(I); Z(I)=ZT; END IF
     K=N/2;  DO WHILE(K<J);  J=J-K;  K=K/2; END DO;  J=J+K
  END DO
  RETURN
END SUBROUTINE ZFFT


SUBROUTINE ZGEIGEN(N,A,W)
  IMPLICIT NONE

  INTEGER N
  COMPLEX*16 A(N,N),VR(N,N),W(N)

  CHARACTER JOBVL, JOBVR
  INTEGER   INFO, LDA, LDVL, LDVR, LWORK
  DOUBLE PRECISION RWORK( 2*N )
  COMPLEX*16 VL( 1, 1 ), WORK( 2*N+1 )

  JOBVL = 'N'   !'V'
  JOBVR = 'V'   !'V'

  LDA = N
  LDVL = 1
  LDVR = N
  LWORK = 2*N+1

  CALL ZGEEV( JOBVL, JOBVR, N, A, LDA, W, VL, LDVL, VR, LDVR, WORK, LWORK, RWORK, INFO )
  IF(INFO/=0)STOP 'ZGEIGEN FAILED'

  A=VR

  RETURN
END SUBROUTINE ZGEIGEN

